
Процедура ОбработкаПроведения(Отказ, Режим)
	
	//Свернем все товары чтобы избежать задвоенных строк
	ТЗТоваров = Товары.Выгрузить(,"Партия, Номенклатура,Количество");
	ТЗТоваров.Свернуть("Партия, Номенклатура","Количество");   
	
	// регистр ПартииТоваров Расход
	Движения.ПартииТоваров.Записывать = Истина;
	Для Каждого ТекСтрокаТовары Из ТЗТоваров Цикл
		
		//Список Партий с остатками в Таблицу значений
		СписокОстатковПартий = Справочники.Партии.ПолучитьСписокОстатковПартий(ТекСтрокаТовары.Номенклатура,
		                                                                       Дата,
																			   Склад,
																			   Организация,
																			   ТекСтрокаТовары.Партия);
		НеобходимыйОстаток   = ТекСтрокаТовары.Количество;
		
		Для Каждого СтрокаПартии из СписокОстатковПартий Цикл
			
			Если НеобходимыйОстаток = 0 Тогда
				Прервать;  //Товар распределился по партиям
			КонецЕсли;  			
			
			Движение = Движения.ПартииТоваров.Добавить();
			Движение.ВидДвижения 	= ВидДвиженияНакопления.Расход;
			Движение.Период 		= Дата;
			Движение.Номенклатура 	= ТекСтрокаТовары.Номенклатура;
			Движение.Склад 			= Склад;
			Движение.Организация 	= Организация;
			
			Движение.Партия 		= СтрокаПартии.Партия;
			
			Если НеобходимыйОстаток <= СтрокаПартии.КоличествоОстаток Тогда  //ИЛИ СтрокаПартии.КоличествоОстаток < 0
				Движение.Количество 	= НеобходимыйОстаток;
				Движение.СуммаПродажи	= 0; // Списание не продаем
				Движение.Сумма			= СтрокаПартии.Партия.ЗакупочнаяЦена * Движение.Количество;
				
				НеобходимыйОстаток 		= НеобходимыйОстаток - Движение.Количество;
				
			ИначеЕсли НеобходимыйОстаток > СтрокаПартии.КоличествоОстаток Тогда
				
				Движение.Количество 	= СтрокаПартии.КоличествоОстаток;
				Движение.СуммаПродажи	= 0; // Все равно не продаем
				Движение.Сумма			= СтрокаПартии.Партия.ЗакупочнаяЦена * Движение.Количество;
				
				НеобходимыйОстаток 		= НеобходимыйОстаток - Движение.Количество;
			КонецЕсли;		
		КонецЦикла;   		
		
		Если НеобходимыйОстаток > 0 Тогда
			
			Если Организация.КонтролироватьОстатки Тогда
				Сообщить("" +Ссылка +Символы.ПС+
				"Нехватает: " +НеобходимыйОстаток+ " " +ТекСтрокаТовары.Номенклатура+ ", Склад: " +Склад+ ", Организация: " +Организация);	
				Отказ = Истина;
			Иначе //Если нехватило остатков, то берем из последней партии (в минус)
				Движение.Количество 	= Движение.Количество + НеобходимыйОстаток;
				Движение.СуммаПродажи	= 0; // Нет. Даже так не продаем
				Движение.Сумма			= СтрокаПартии.Партия.ЗакупочнаяЦена * Движение.Количество;
			КонецЕсли;		
		КонецЕсли;  		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Справочники.Партии.УдалениеИлиОтменаУделенияПартийДокумента(Ссылка,ПометкаУдаления);		
	
	СуммаДокумента = 0;	
	Для каждого СтрокаТЧ из Товары Цикл
		
		Если ЗначениеЗаполнено(СтрокаТЧ.Партия) Тогда
			СуммаДокумента = СуммаДокумента + СтрокаТЧ.Партия.ЗакупочнаяЦена * СтрокаТЧ.Количество;
		КонецЕсли;
				
	КонецЦикла;
	    	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)	
	
	Для Каждого Стр из Товары Цикл 		
		Стр.Партия	= Неопределено;
		
		Если ЗначениеЗаполнено(Стр.Номенклатура) Тогда
			Стр.Партия = Справочники.Партии.СоздатьНовуюПартию(Стр.Номенклатура).Ссылка;
		КонецЕсли;
		
	КонецЦикла;		
КонецПроцедуры


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИнвентаризацияТМЦ") Тогда
		// Заполнение шапки
		Организация       = ДанныеЗаполнения.Организация;
		Ответственный     = ПараметрыСеанса.ТекущийПользователь;
		Склад             = ДанныеЗаполнения.Склад;
		ИнвентаризацияТМЦ = ДанныеЗаполнения.Ссылка;
		
		Для Каждого ТекСтрокаТовары Из ДанныеЗаполнения.Товары Цикл
			
			Если ТекСтрокаТовары.Количество < ТекСтрокаТовары.КоличествоУчет Тогда
			
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Номенклатура     = ТекСтрокаТовары.Номенклатура;
				НоваяСтрока.ЕдиницаИзмерения = ТекСтрокаТовары.ЕдиницаИзмерения;
				НоваяСтрока.Количество       = ТекСтрокаТовары.КоличествоУчет - ТекСтрокаТовары.Количество;
				НоваяСтрока.Партия           = ТекСтрокаТовары.Партия;
			
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

